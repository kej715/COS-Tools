         TITLE     'VSTORE'
         SUBTITLE  'Vector Store'
         IDENT     VSTORE
         COMMENT   'VSTORE - Vector Store'

VECTOR   SECTION   MIXED

**
*  @%vstore - vector store
*
*  Entry:
*    (A7+0) - address of integer identifying source register
*    (A7+1) - address of integer identifying span value
*    (A7+2) - address of destination array
*
*  Exit:
*    S7 - integer identifying source register
*
         ENTRY     @%vstore
@%vstore BSS       0
         S2        INSTR,         ; template instruction
         S1        <6             ; mask to clear register numbers
         S1        S1<48
         S2        #S1&S2
         S1        <3             ; mask for register number
         A1        0,A7           ; get source register number
         S3        ,A1
         S3        S3&S1
         S7        S3             ; result to be returned
         S3        S3<(3+48)      ; position and merge it into instruction
         S2        S2!S3
         A1        1,A7           ; get span value
         A2        ,A1
         A0        A2-1
         JAZ       @%vst1         ; if span value is 1
         S3        2              ; indicate span value in register A2
         S3        S3<48
         S2        S2!S3
@%vst1   INSTR,    S2             ; store constructed instruction
         A0        2,A7           ; get address of destination array
INSTR    BSS       0
         ,A0,A1    V1             ; replaced with constructed instruction
         J         B00

         SECTION   *

         END
