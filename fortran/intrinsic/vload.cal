         TITLE     'VLOAD'
         SUBTITLE  'Vector Load'
         IDENT     VLOAD
         COMMENT   'VLOAD - Vector Load'

VECTOR   SECTION   MIXED

**
*  @%vload - vector load
*
*  Entry:
*    (A7+0) - address of integer identifying result register
*    (A7+1) - address of integer identifying span value
*    (A7+2) - address of source array
*
*  Exit:
*    S7 - integer identifying result register
*
         ENTRY     @%vload
@%vload  BSS       0
         S2        INSTR,         ; template instruction
         S1        <9             ; mask to clear register numbers
         S1        S1<48
         S2        #S1&S2
         S1        <3             ; mask for register number
         A1        0,A7           ; get result register number
         S3        ,A1
         S3        S3&S1
         S7        S3             ; result to be returned
         S3        S3<(6+48)      ; position and merge it into instruction
         S2        S2!S3
         A1        1,A7           ; get span value
         A2        ,A1
         A0        A2-1
         JAZ       @%vlo1         ; if span value is 1
         S3        2              ; indicate span value in register A2
         S3        S3<48
         S2        S2!S3
@%vlo1   INSTR,    S2             ; store constructed instruction
         A0        2,A7           ; get address of source array
INSTR    BSS       0
         V1        ,A0,A1         ; replaced with constructed instruction
         J         B00

         SECTION   *

         END
