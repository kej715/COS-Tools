         TITLE     'COPYD'
         SUBTITLE  'Copy Blocked Dataset'
         IDENT     COPYD
         COMMENT   'COPYD - Copy Blocked Dataset'
         ENTRY     COPYD
         EXT       $D2I@24,$I2D@24,$PACK,$PARGS,$UNPACK
         START     COPYD

NB       =         D'10
UDASZ    =         NB*D'512

DATA     SECTION   DATA

IODN     DATA      '$IN'L
         VWD       6/4,30/0,4/OSTSA,24/0 ; system managed/resident

OODN     DATA      '$OUT'L
         VWD       6/4,30/0,4/OSTSA,24/0 ; system managed/resident

         SECTION   *

TEXT     SECTION   CODE

COPYD    =         *
         A1        ARGV           ; parse command line arguments
         R         $PARGS
         S1        ARGI,          ; set input dataset name
         IODN,     S1
         S1        ARGO,          ; set output dataset name
         OODN,     S1
         OPEN      IODN,I         ; open input file
         JSN       COP7           ; if open failed
         OPEN      OODN,O         ; open output file
         JSN       COP8           ; if open failed
         S1        ARGS,          ; convert S value to binary
         R         $D2I@24
         S1        132
         S0        S1-S7
         JSM       COP9           ; if invalid argument
         SHIFT,    S7
         S0        S7
         JSN       COP10          ; if shifting text file

COP1     A1        IODN,
         READP     A1,UDA,UDASZ
         JSZ       COP3           ; if EOF, EOD, null record, or error
         JSM       COP2           ; if EOR
         A3        A4-A2          ; number of words read
         A1        OODN,
         WRITEP    A1,UDA,A3
         S0        S1
         JSZ       COP1           ; if no error
         J         COP6

COP2     A3        A4-A2          ; number of words read
         A1        OODN,
         WRITE     A1,UDA,A3,S6
         S0        S1
         JSN       COP6           ; if error
         A1        RCOUNT,        ; increment count of records
         A1        A1+1
         RCOUNT,   A1
         J         COP1

COP3     S0        S1
         JSN       COP5           ; if unrecoverable read error
         S1        DPCWF,A1       ; determine whether EOF or EOD
         S2        <2
         S1        S1>(D'63-D'03)
         S1        S1&S2
         S0        S2-S1
         JSZ       COP4           ; if EOD
         A1        OODN,
         WRITEF    A1
         S0        S1
         JSN       COP6           ; if error
         A1        FCOUNT,        ; increment count of files
         A1        A1+1
         FCOUNT,   A1
         J         COP1

COP4     A1        OODN,
         WRITED    A1
         S0        S1
         JSN       COP6           ; if error
         A1        RCOUNT,
         S7        =' ',
         R         $I2D@24
         COPB,     S7
         A1        FCOUNT,
         S7        =' ',
         R         $I2D@24
         COPC,     S7
         MESSAGE   COPA,US
         ENDP

COP5     MESSAGE   ='Unrecoverable read error'Z,US
         ABORT

COP6     MESSAGE   ='Unrecoverable write error'Z,US
         ABORT

COP7     S1        ARGI,
         COPE,     S1
         MESSAGE   COPD,US
         ABORT

COP8     S1        ARGO,
         COPE,     S1
         MESSAGE   COPD,US
         ABORT

COP9     MESSAGE   COPF,US
         ABORT
*
*  Handle the input file as text. Shift each line by the number of spaces
*  specified by the S= command line argument.
*
*    A3 : number of words read
*    S6 : unused bit count
*
COP10    A1        IODN,
         READ      A1,UDA,UDASZ
         JSZ       COP3           ; if EOF, EOD, null record, or error
         JSM       COP11          ; if EOR
         S6        0              ; clear unused bit count

COP11    A1        A4-A2          ; number of words read
         S7        7              ; convert number of unused bits to unused bytes
         S6        S6+S7
         S6        S6>3
         S7        A1             ; compute maximum number of characters to process
         S7        S7<3
         S7        S7-S6
         S1        SHIFT,         ; calculate total record length including shift count
         S1        S1+S7
         S2        132
         S0        S2-S1
         JSP       COP12          ; if total record length won't exceed 132 characters
         S2        S1-S2          ; reduce number of characters to process
         S7        S7-S2

COP12    CHARS,    S7             ; save character count
         A1        SHIFT,         ; generate line prefix for shift
         A2        LBUF+1
         S1        ' 'R

COP13    A1        A1-1
         A0        A1
         JAZ       COP14          ; if line prefix complete
         ,A2       S1
         A2        A2+1
         J         COP13

COP14    A1        UDA            ; unpack record to line buffer
         A3        CHARS,
         R         $UNPACK
         A3        CHARS,         ; calculate total record length
         A4        SHIFT,
         A3        A3+A4
         CHARS,    A3
         A1        LBUF           ; pack shifted record
         A2        UDA
         R         $PACK
         S1        CHARS,         ; calculate number of words in packed record
         S2        7
         S1        S1+S7
         S1        S1>3
         A3        S1             ; number of words to write
         S6        CHARS,         ; calculate number of unused bytes in last word
         S7        <3
         S6        S6&S7
         S7        8
         S6        S7-S6
         S6        S6<3           ; number of unused bits
         A1        OODN,
         WRITE     A1,UDA,A3,S6
         S0        S1
         JSN       COP6           ; if error
         A1        RCOUNT,        ; increment count of records
         A1        A1+1
         RCOUNT,   A1
         S1        ' 'R           ; replace page eject character
         LBUF,     S1
         J         COP10

         SECTION   *

DATA     SECTION   DATA

COPA     DATA      'Copy complete. Records: '
COPB     DATA      0
         DATA      ' Files: '
COPC     DATA      0
         CON       0

COPD     DATA      'Failed to open'
COPE     DATA      0
         CON       0

COPF     DATA      'Invalid argument'Z
         CON       0

ARGV     DATA      'I'L,0,'IN$'L
ARGI     DATA      0
         DATA      'O'L,0,'OUT$'L
ARGO     DATA      0
         DATA      'S'L,0,'0'L
ARGS     DATA      0
         CON       0

CHARS    CON       0
SHIFT    CON       0
RCOUNT   CON       0
FCOUNT   CON       0

UDA      BSS       UDASZ

LBUF     DATA      '1'R           ; page eject replaced with line advance after first record written
         BSS       131

         SECTION   *

         END
