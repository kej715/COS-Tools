         TITLE     'COPYD'
         SUBTITLE  'Copy Blocked Dataset'
         IDENT     COPYD
         COMMENT   'COPYD - Copy Blocked Dataset'
         ENTRY     COPYD
         EXT       $I2D@24,$PARGS
         START     COPYD

NB       =         D'10
UDASZ    =         NB*D'512
BUFSZ    =         NB*D'512

DATA     SECTION   DATA

IDSP     DSP       $IN,IBUF,NB
IODN     DATA      '$IN'L
         VWD       6/4,30/0,4/OSTSA,24/IDSP ; system managed/resident
*        VWD       36/0,4/OSTSA,24/IDSP     ; system managed/resident

ODSP     DSP       $OUT,OBUF,NB
OODN     DATA      '$OUT'L
         VWD       6/4,30/0,4/OSTSA,24/ODSP ; system managed/resident
*        VWD       36/0,4/OSTSA,24/ODSP     ; system managed/resident

         SECTION   *

TEXT     SECTION   CODE

COPYD    =         *
         A1        ARGS           ; parse command line arguments
         R         $PARGS
         S1        ARGI,          ; set input dataset name
         IDSP,     S1
         IODN,     S1
         S1        ARGO,          ; set output dataset name
         ODSP,     S1
         OODN,     S1
         OPEN      IODN,I         ; open input file
         JSN       COP7           ; if open failed
         OPEN      OODN,O         ; open output file
         JSN       COP8           ; if open failed

COP1     READP     IDSP,UDA,UDASZ
         JSM       COP2           ; if EOR
         JSZ       COP3           ; if EOF, EOD, null record, or error
         A3        A4-A2          ; number of words read
         WRITEP    ODSP,UDA,A3
         S0        S1
         JSZ       COP1           ; if no error
         J         COP6

COP2     A3        A4-A2          ; number of words read
         WRITE     ODSP,UDA,A3,S6
         S0        S1
         JSN       COP6           ; if no error
         A1        RCOUNT,        ; increment count of records
         A1        A1+1
         RCOUNT,   A1
         J         COP1

COP3     S0        S1
         JSN       COP5           ; if unrecoverable read error
         S1        IDSP+DPCWF,    ; determine whether EOF or EOD
         S2        <2
         S1        S1>(D'63-D'03)
         S1        S1&S2
         S0        S2-S1
         JSZ       COP4           ; if EOD
         WRITEF    ODSP
         S0        S1
         JSN       COP6           ; if error
         A1        FCOUNT,        ; increment count of files
         A1        A1+1
         FCOUNT,   A1
         J         COP1

COP4     WRITED    ODSP
         S0        S1
         JSN       COP6           ; if error
         A1        RCOUNT,
         S7        =' ',
         R         $I2D@24
         COPB,     S7
         A1        FCOUNT,
         S7        =' ',
         R         $I2D@24
         COPC,     S7
         MESSAGE   COPA,US
         ENDP

COP5     MESSAGE   ='Unrecoverable read error'Z,US
         ABORT

COP6     MESSAGE   ='Unrecoverable write error'Z,US
         ABORT

COP7     S1        ARGI,
         COPE,     S1
         MESSAGE   COPD,US
         ABORT

COP8     S1        ARGO,
         COPE,     S1
         MESSAGE   COPD,US
         ABORT

         SECTION   *

DATA     SECTION   DATA

COPA     DATA      'Copy complete. Records: '
COPB     DATA      0
         DATA      ' Files: '
COPC     DATA      0
         CON       0

COPD     DATA      'Failed to open'
COPE     DATA      0
         CON       0

ARGS     DATA      'I'L,0,'IN$'L
ARGI     DATA      0
         DATA      'O'L,0,'OUT$'L
ARGO     DATA      0
         CON       0

RCOUNT   CON       0
FCOUNT   CON       0

IBUF     BSS       BUFSZ
OBUF     BSS       BUFSZ
UDA      BSS       UDASZ

         SECTION   *

         END
